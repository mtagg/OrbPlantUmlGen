<?xml version="1.0" encoding="utf-8"?>
<TcPlcObject Version="1.1.0.1" ProductVersion="3.1.4024.12">
  <POU Name="FB_Joint" Id="{307cd199-986b-4cc3-817e-880c8d32dec0}" SpecialFunc="None">
    <Declaration><![CDATA[FUNCTION_BLOCK FB_Joint EXTENDS FB_StateMachine IMPLEMENTS  I_StateMachine
VAR_INPUT
	bStartSetup : BOOL;
	eJointType : E_JointType;
END_VAR
VAR_OUTPUT
END_VAR
VAR
	stAxis		: ST_JointAxis;
	eInnerState : E_JointStates;
	fbSetup : FB_JointSetup;
END_VAR
]]></Declaration>
    <Implementation>
      <ST><![CDATA[OuterLopSM();]]></ST>
    </Implementation>
    <Folder Name="Properties" Id="{9eefb641-57da-4e7e-b09c-80341f3a9315}" />
    <Property Name="Calibrated" Id="{2febfed9-fb0a-4d7a-ad83-07afe80112ee}" FolderPath="Properties\">
      <Declaration><![CDATA[PROPERTY Calibrated : BOOL]]></Declaration>
      <Get Name="Get" Id="{1060293c-18a3-4452-ab91-36347e3b2eaf}">
        <Declaration><![CDATA[VAR
END_VAR
]]></Declaration>
        <Implementation>
          <ST><![CDATA[IF bActive THEN
	Calibrated := NOT fbSetup.SetupNeeded;
ELSE
	Calibrated :=  TRUE;
END_IF ]]></ST>
        </Implementation>
      </Get>
    </Property>
    <Method Name="Disable" Id="{ad3267b8-d7ad-458f-8882-df0c9cfabaff}">
      <Declaration><![CDATA[METHOD Disable : BOOL

VAR
	bChildBusy  : BOOL := FALSE;
END_VAR]]></Declaration>
      <Implementation>
        <ST><![CDATA[fbSetup(bActive:=FALSE);



IF fbSetup.Busy THEN
	bChildBusy := TRUE;
ELSIF fbSetup.bError THEN
	bChildBusy := TRUE;
	{warning 'TODO: trigger e-stop'}
	//	Stack trace and bError switching are reserved for inner loop
	fbSetup.ErrorAck:=TRUE;
END_IF



IF bChildBusy THEN
	Disable := FALSE;
ELSE
	Disable := TRUE;
END_IF]]></ST>
      </Implementation>
    </Method>
    <Method Name="InnerLoopSM" Id="{85132190-b9cd-4181-b267-42b90705d68b}">
      <Declaration><![CDATA[METHOD InnerLoopSM : BOOL
]]></Declaration>
      <Implementation>
        <ST><![CDATA[// update all children FBs
stAxis.stAbsEncoderAxisRef.ReadStatus();
stAxis.stIncEncoderAxisRef.ReadStatus();
fbSetup();

CASE CyclicInnerCase(TO_STRING(eInnerState)) OF
	E_JointStates.Idle:
		IF bStartSetup THEN
			bStartSetup := FALSE;
			ChangeInnerState(E_JointStates.Setup1);
		END_IF

	E_JointStates.Setup1:
		IF fbSetup.Busy THEN
			// fbSetup should never be busy here 	
			sErrorDetails := 'fbSetup.Busy = TRUE';
			fbEventHandler(eNapID:=E_Event.UNABLE_TO_START_TASK,sMessage:=sErrorDetails);
			bError := TRUE;	
		ELSE
			IF fbSetup.bError THEN
				sErrorDetails := 'fbSetup.bReady=FALSE';
				fbEventHandler(eNapID:=E_Event.UNABLE_TO_START_TASK,sMessage:=sErrorDetails);
				bError := TRUE;
			ELSE
				fbSetup.bStart:=TRUE;
				ChangeInnerState(E_JointStates.Setup2);
			END_IF
		END_IF
		
	E_JointStates.Setup2:
		IF fbSetup.Busy THEN
			// Wait... fbSetup still busy 			
		ELSE
			IF fbSetup.bError THEN
				fbEventHandler(eNapID:=E_Event.TASK_FAILED,sTrace:=fbSetup.EventTrace);
				fbSetup.ErrorAck:=TRUE;
				bError := TRUE;
			ELSE
				IF Calibrated THEN
					fbEventHandler(eNapID:=E_Event.REQUEST_COMPLETED);
					ChangeInnerState(E_JointStates.Idle);
				ELSE
					sErrorDetails := 'Calibrated=FALSE';
					fbEventHandler(eNapID:=E_Event.TASK_FAILED,sMessage:=sErrorDetails);
					bError := TRUE;
				END_IF
			END_IF
		END_IF
END_CASE
]]></ST>
      </Implementation>
    </Method>
    <Method Name="Reset" Id="{daabd21e-bb3d-4610-83aa-92ac7a03e1d7}">
      <Declaration><![CDATA[METHOD Reset : BOOL
]]></Declaration>
      <Implementation>
        <ST><![CDATA[// Reactivate children FBs
fbSetup.Active:=TRUE;]]></ST>
      </Implementation>
    </Method>
    <Method Name="Setup" Id="{b037566a-333a-4807-8a2a-28e4378c08ab}">
      <Declaration><![CDATA[METHOD Setup : BOOL
]]></Declaration>
      <Implementation>
        <ST><![CDATA[eStateMachineSourceID := eStateSourceID;
CASE eJointType OF 
	E_JointType.DualEncoderWithOffset:
		stAxis.bDualEncoderJoint := TRUE;
		stAxis.bVariableOffsetJoint := TRUE;
	E_JointType.DualEncoderNoOffset:	
		stAxis.bDualEncoderJoint := TRUE;
	E_JointType.SingleAbsoluteEncoder:
		stAxis.bSingleAbsoluteJoint  := TRUE;
	E_JointType.SingleIncrementalEncoder:
		stAxis.bSingleIncrementalJoint  := TRUE;
END_CASE

fbSetup(eStateSourceID := THIS^.eStateMachineSourceID +  E_JointID_Offset.JointSetup,
					aErrorLog:=THIS^.aErrorLog,
					nErrorLogIndex:=THIS^.nErrorLogIndex,
					stAxis:=THIS^.stAxis);
					
SetInnerEnumeration(nStateEnum:=THIS^.eInnerState);]]></ST>
      </Implementation>
    </Method>
    <LineIds Name="FB_Joint">
      <LineId Id="9" Count="0" />
    </LineIds>
    <LineIds Name="FB_Joint.Calibrated.Get">
      <LineId Id="2" Count="0" />
      <LineId Id="6" Count="1" />
      <LineId Id="9" Count="0" />
      <LineId Id="5" Count="0" />
    </LineIds>
    <LineIds Name="FB_Joint.Disable">
      <LineId Id="8" Count="0" />
      <LineId Id="21" Count="1" />
      <LineId Id="20" Count="0" />
      <LineId Id="9" Count="0" />
      <LineId Id="35" Count="2" />
      <LineId Id="44" Count="0" />
      <LineId Id="39" Count="0" />
      <LineId Id="38" Count="0" />
      <LineId Id="32" Count="0" />
      <LineId Id="4" Count="0" />
      <LineId Id="24" Count="1" />
      <LineId Id="23" Count="0" />
      <LineId Id="26" Count="0" />
      <LineId Id="28" Count="0" />
      <LineId Id="30" Count="0" />
      <LineId Id="27" Count="0" />
    </LineIds>
    <LineIds Name="FB_Joint.InnerLoopSM">
      <LineId Id="5" Count="1" />
      <LineId Id="9" Count="0" />
      <LineId Id="8" Count="0" />
      <LineId Id="10" Count="6" />
      <LineId Id="19" Count="15" />
      <LineId Id="17" Count="0" />
      <LineId Id="35" Count="1" />
      <LineId Id="39" Count="16" />
      <LineId Id="37" Count="0" />
      <LineId Id="18" Count="0" />
      <LineId Id="4" Count="0" />
    </LineIds>
    <LineIds Name="FB_Joint.Reset">
      <LineId Id="5" Count="0" />
      <LineId Id="4" Count="0" />
    </LineIds>
    <LineIds Name="FB_Joint.Setup">
      <LineId Id="4" Count="0" />
      <LineId Id="22" Count="0" />
      <LineId Id="16" Count="0" />
      <LineId Id="20" Count="0" />
      <LineId Id="17" Count="0" />
      <LineId Id="23" Count="5" />
      <LineId Id="12" Count="0" />
      <LineId Id="11" Count="0" />
      <LineId Id="6" Count="4" />
      <LineId Id="5" Count="0" />
    </LineIds>
  </POU>
</TcPlcObject>