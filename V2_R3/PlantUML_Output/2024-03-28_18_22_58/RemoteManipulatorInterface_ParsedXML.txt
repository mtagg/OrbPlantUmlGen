	>>
	>>Start of Method : InnerLoopSM
	>>
	>>
	CASE CyclicInnerCase(TO_STRING(eInnerState)) OF
		E_RemoteManipulatorInterfaceStates.Idle:
			IF bEstablishComm THEN
				bEstablishComm := FALSE;
				tcpclient.bActive := TRUE;
				ChangeInnerState(E_RemoteManipulatorInterfaceStates.Connecting1);
			END_IF
		E_RemoteManipulatorInterfaceStates.Connecting1:
			fbConnectTON( IN := FALSE );
			fbDataExchaTON( IN := FALSE );
			fbConnectTON( IN := TRUE, PT := T#5S );
			nCycleID := 1;
			stProcessedDataToRM.nCycleID := nCycleID;
			stDummyDataToRM.nCycleID := nCycleID;
			ChangeInnerState(E_RemoteManipulatorInterfaceStates.Connecting2);
		E_RemoteManipulatorInterfaceStates.Connecting2:	
			fbConnectTON();
			tcpclient(bSend:=TRUE, stSendData:=stDummyDataToRM, stReceiveData=>stDataFromKuka);
			IF SyncRM() THEN
				fbConnectTON( IN := FALSE );
				ChangeInnerState(E_RemoteManipulatorInterfaceStates.DataExchange1);
			ELSE
				IF fbConnectTON.Q THEN
					fbConnectTON( IN := FALSE );
					fbEventHandler(eNapID:=E_Event.TCP_CONNECTION_TIMEOUT);
					bError:=TRUE;
				END_IF
			END_IF
		E_RemoteManipulatorInterfaceStates.DataExchange1:
			IF bEndCommunication THEN
				bEndCommunication := FALSE;
				IF Connected THEN
					ChangeInnerState(E_RemoteManipulatorInterfaceStates.CloseConnection1);
				ELSE
					fbEventHandler(eNapID:=E_Event.SYSTEM_IN_UNEXPECTED_STATE, sMessage:='Close Requested while bConnected=FALSE');
					bError:=TRUE;
				END_IF
			ELSE
				IF Connected THEN
					nCycleID := nCycleID + 1;
					stProcessedDataToRM.nCycleID := UINT_TO_LREAL(nCycleID);
					stDummyDataToRM.nCycleID := UINT_TO_LREAL(nCycleID);
					IF bSendMotionCommands THEN
						bSendMotionCommands := FALSE;
						stDummyDataToRM := stProcessedDataToRM;
						tcpclient(bSend:=TRUE, stSendData := stProcessedDataToRM, stReceiveData => stDataFromKuka);
					ELSE
						tcpclient(bSend:=TRUE, stSendData := stDummyDataToRM, stReceiveData => stDataFromKuka);
					END_IF
				ELSE
					tcpclient(bSend:=TRUE, stSendData := stDummyDataToRM, stReceiveData => stDataFromKuka);
					fbEventHandler(eNapID:=E_Event.TCP_TEMPORARY_CONNECTION_LOST);
					fbDataExchaTON( IN := TRUE, PT := T#3S );
					ChangeInnerState(E_RemoteManipulatorInterfaceStates.DataExchange2);
				END_IF
			END_IF
			IF CycleIdDiscrepency() THEN
				sErrorDetails := CONCAT('WS_ID-RM_ID = ',LREAL_TO_STRING(nCycleID-stDataFromKuka.nCycleID));
				fbEventHandler(eNapID:=E_Event.TCP_CYCLE_ID_HIGH_DISCREPANCY, sMessage:=sErrorDetails);
				fbDataExchaTON( IN := TRUE, PT := T#3S );
				ChangeInnerState(E_RemoteManipulatorInterfaceStates.DataExchange2);
			END_IF
			ResetTimeDerivatives(); 
		E_RemoteManipulatorInterfaceStates.DataExchange2:
			fbDataExchaTON();
			IF SyncRM() THEN
				fbDataExchaTON( IN := FALSE );
				ChangeInnerState(E_RemoteManipulatorInterfaceStates.DataExchange1);
			ELSIF fbDataExchaTON.Q THEN
				fbDataExchaTON( IN := FALSE );
				fbEventHandler(eNapID:=E_Event.TCP_CONNETION_LOST);
				bError:=TRUE;			
			END_IF
		E_RemoteManipulatorInterfaceStates.CloseConnection1:
			IF Connected THEN
				tcpclient(bActive:=FALSE);
			ELSE
				ChangeInnerState(E_RemoteManipulatorInterfaceStates.Idle);
			END_IF
	END_CASE


States found:
	eInnerState
Methods Found:
	InnerLoopSM
Method-Case Pairs Found:
