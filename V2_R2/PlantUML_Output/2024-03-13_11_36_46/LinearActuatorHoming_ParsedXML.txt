	>>
	>>Start of Method : InnerLoopSM
	>>
	>>
	CASE CyclicInnerCase(TO_STRING(eInnerState)) OF
		E_LinearActuatorHomingStates.Idle:
			IF bStart THEN
				bStart := FALSE;
				IF HomingNeeded THEN
					ChangeInnerState(E_LinearActuatorHomingStates.CheckIfEnabled);
				ELSE
				END_IF
			END_IF
		E_LinearActuatorHomingStates.CheckIfEnabled:
			bEnabledBeforeHoming := fbPower.Status; 
			ChangeInnerState(E_LinearActuatorHomingStates.ResetAxis1);
		E_LinearActuatorHomingStates.ResetAxis1:
			fbReset(Axis:=stParent.stAxisRef,Execute:=TRUE);
			nPLC_Cycles := 0;
			ChangeInnerState(E_LinearActuatorHomingStates.ResetAxis2);
		E_LinearActuatorHomingStates.ResetAxis2:
			nPLC_Cycles := nPLC_Cycles +1; 
			IF fbReset.Done THEN
				fbReset(Axis:=stParent.stAxisRef,Execute:=FALSE);  
				IF bEnabledBeforeHoming THEN
					ChangeInnerState(E_LinearActuatorHomingStates.MoveToTop);
				ELSE
					ChangeInnerState(E_LinearActuatorHomingStates.EnableAxis1);
				END_IF
			ELSIF fbReset.Error THEN
				fbEventHandler(nBeckhoffID:=fbReset.ErrorID);
				bError := TRUE;
			ELSE
				IF nPLC_Cycles > SystemConfig.nLINEAR_ACTUATOR_MAX_CYCLES_TO_RESET THEN
					fbEventHandler(eNapID:=E_Event.RESET_NOT_DONE_IN_ALLOTED_TIME);
					bError := TRUE;
				ELSE
				END_IF
			END_IF
		E_LinearActuatorHomingStates.EnableAxis1:
			fbPower(Axis:=stParent.stAxisRef,Enable:=TRUE, Enable_Positive := TRUE, Enable_Negative := TRUE);
			nPLC_Cycles := 0;
			ChangeInnerState(E_LinearActuatorHomingStates.EnableAxis2);
		E_LinearActuatorHomingStates.EnableAxis2:
			nPLC_Cycles := nPLC_Cycles +1; 
			IF fbPower.Error THEN
				fbEventHandler(nBeckhoffID:=fbPower.ErrorID);
				bError := TRUE;
			ELSIF fbPower.Status = TRUE THEN 
				fbReset(Axis:=stParent.stAxisRef,Execute:=FALSE); 
				ChangeInnerState(E_LinearActuatorHomingStates.MoveToTop);
			ELSE
				IF nPLC_Cycles > SystemConfig.nLINEAR_ACTUATOR_MAX_CYCLES_TO_ENABLE THEN
					fbEventHandler(eNapID:=E_Event.ENABLE_NOT_DONE_IN_ALLOTED_TIME);
					bError := TRUE;
				ELSE
				END_IF
			END_IF
		E_LinearActuatorHomingStates.MoveToTop:
			fbJog(Axis:=stParent.stAxisRef, Mode:=MC_JOGMODE_CONTINOUS, JogForward:=TRUE, JogBackwards:=FALSE,Velocity:=SystemConfig.fLINEAR_ACTUATOR_HOMING_FAST_SPEED);
			IF fbJog.Error THEN
				fbEventHandler(nBeckhoffID:=fbJog.ErrorID);
				bError := TRUE;
			ELSIF fbJog.CommandAborted THEN
				fbEventHandler(eNapID:=E_Event.MC2_JOG_COMMAND_ABORTED);
				bError := TRUE;
			ELSE
				IF fbJog.Done THEN
					sErrorDetails := 'fbJog.Done=TRUE unexpectedly';
					fbEventHandler(eNapID:=E_Event.TASK_WARNING,sMessage:=sErrorDetails);
				END_IF
				IF stParent.bAtTopPosition THEN
					fbJog(Axis:=stParent.stAxisRef, JogForward:=FALSE, JogBackwards:=FALSE); 
					ChangeInnerState(E_LinearActuatorHomingStates.WaitingForHalt);
				ELSE 
				END_IF
			END_IF
		E_LinearActuatorHomingStates.WaitingForHalt:
			fbHalt(Axis:=stParent.stAxisRef,Execute:=TRUE);
			IF fbHalt.Done THEN
				fbHalt(Axis:=stParent.stAxisRef,Execute:=FALSE);
				ChangeInnerState(E_LinearActuatorHomingStates.SettingHomePos);
			ELSIF fbHalt.Error THEN
				fbEventHandler(nBeckhoffID:=fbHalt.ErrorID);
				bError := TRUE;
			ELSE
			END_IF
		E_LinearActuatorHomingStates.SettingHomePos:
			fbHome(Axis:=stParent.stAxisRef, Execute:=TRUE, Position:=0, HomingMode:=MC_HomingMode.MC_Direct); 
			IF fbHome.Error THEN
				fbEventHandler(nBeckhoffID:=fbHome.ErrorID);
				bError := TRUE;
			ELSIF fbHome.CommandAborted THEN
				fbEventHandler(eNapID:=E_Event.MC2_HOME_COMMAND_ABORTED);
				bError := TRUE;
			ELSIF fbHome.Done THEN
				fbHome(Axis:=stParent.stAxisRef, Execute:=FALSE); 
				IF bEnabledBeforeHoming THEN
					ChangeInnerState(E_LinearActuatorHomingStates.WrapUp);
				ELSE
					ChangeInnerState(E_LinearActuatorHomingStates.DisableAxis);
				END_IF
			ELSE
			END_IF
		E_LinearActuatorHomingStates.DisableAxis:
			fbPower(Axis:=stParent.stAxisRef,Enable:=FALSE);
			IF fbPower.Error THEN
				fbEventHandler(nBeckhoffID:=fbPower.ErrorID);
				bError := TRUE;
			ELSIF fbPower.Status = FALSE THEN 
				ChangeInnerState(E_LinearActuatorHomingStates.WrapUp);
			ELSE
			END_IF
		E_LinearActuatorHomingStates.WrapUp:
			stParent.bForceHoming := FALSE;
			bCompleted := TRUE;
			dtTimeCompleted := DateTime.GVL_DATE_TIME;
			ChangeInnerState(E_LinearActuatorHomingStates.Idle);
	END_CASE


States found:
	eInnerState
Methods Found:
	InnerLoopSM
Method-Case Pairs Found:
